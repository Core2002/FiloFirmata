apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'net.java.dev.jna', name: 'jna', version: '4.0.0'
    //compile group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.13'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.13'
    compile group: 'net.jodah', name: 'typetools', version: '0.4.4'
    compile group: 'com.google.guava', name: 'guava', version: '19.0'

    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'com.jayway.awaitility', name: 'awaitility', version: '1.7.0'
}


group 'com.bortbort.arduino'
archivesBaseName = "filo-firmata"
version '0.1.10'
sourceCompatibility = 1.8



sourceSets {
    main {
        java {
            srcDir 'lib/purejavacomm/src'
            srcDir 'lib/JavaHelpers/src/main/java'
        }
    }
}





task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


artifacts {
    archives javadocJar, sourcesJar
}


signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom {
                project {
                    name 'FiloFirmata'
                    packaging 'jar'
                    description 'FiloFirmata is a client library for the Firmata protocol used with hardware project boards.'
                    url 'https://github.com/reapzor/FiloFirmata'

                    scm {
                        url 'scm:git@github.com:reapzor/FiloFirmata.git'
                        connection 'scm:git@github.com:reapzor/FiloFirmata.git'
                        developerConnection 'scm:git@github.com:reapzor/FiloFirmata.git'
                    }

                    licenses {
                        license {
                            name 'The MIT License (MIT)'
                            url 'https://github.com/reapzor/FiloFirmata/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'reapzor'
                            name 'Chuck Benson'
                        }
                    }
                }
            }.writeTo("pom.xml")
        }
    }
}
